plugins {
    id 'nebula.kotlin' version '1.2.41'
    id 'nebula.project' version '3.4.0'
    id 'nebula.release' version '6.3.0'
    id 'nebula.maven-publish' version '7.0.1'
    id 'nebula.nebula-bintray' version '3.5.2'
}

group 'au.com.console'

repositories {
    jcenter()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    compile("org.jetbrains.kotlin:kotlin-reflect:1.2.41")
    compile 'org.springframework.data:spring-data-jpa:2.0.9.RELEASE'
    compileOnly 'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.2.Final'

    testCompile 'org.springframework.boot:spring-boot-starter-data-jpa:2.0.4.RELEASE'
    testCompile 'com.h2database:h2:1.4.191'
    testCompile 'org.springframework.boot:spring-boot-starter-test:2.0.4.RELEASE'
    testCompile("org.junit.jupiter:junit-jupiter-api:5.2.0")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.2.0")
}

tasks.bintrayUpload.dependsOn tasks.check
tasks.artifactoryPublish.dependsOn tasks.check

gradle.taskGraph.whenReady { graph ->
    tasks.bintrayUpload.onlyIf {
        graph.hasTask(':final') || graph.hasTask(':candidate')
    }
    tasks.artifactoryPublish.onlyIf {
        graph.hasTask(':snapshot') || graph.hasTask(':devSnapshot')
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['nebula']
    // allow republshing in case bintray fails to upload properly
    override = true
    pkg {
        repo = 'kotlin'
        userOrg = 'consoleau'
        name = 'kotlin-jpa-specification-dsl'
        licenses = ['Apache-2.0']
        labels = ['kotlin','spring']
        websiteUrl = "https://github.com/consoleau/${project.name}"
        issueTrackerUrl = "https://github.com/consoleau/${project.name}/issues"
        vcsUrl = "https://github.com/consoleau/${project.name}.git"
    }
}

task wrapper(type: Wrapper) {
    distributionUrl = "https://nexus.ops.console.com.au/repository/gradle-distributions/gradle-4.7-bin.zip"
}
